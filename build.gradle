plugins {
    id "scala"
    // test runner for scalatest
    id "com.github.maiflai.scalatest" version "0.22"
    // create a "fat jar" similar to the sbt assembly plugin
    id "com.github.johnrengelman.shadow" version "4.0.2"
    // make application runnable from the terminal with a `run` task
    id "application"
}

// inform the 'application' plugin where the app entry point is located
mainClassName = "com.starter.Main"

shadowJar {
    // creates a fat jar at build/libs/scala-gradle-starter-all.jar
    baseName = "scala-gradle-starter"
}

repositories {
    mavenCentral()
}

ext {
    scalaVersion = "2.12.8"
    scalacVersion = "2.12"
    log4jVersion = "2.9.1"
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "org.scala-lang:scala-reflect:$scalaVersion"

    // logging
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    runtime "org.apache.logging.log4j:log4j-core:$log4jVersion"
    runtime "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"

    // test
    testCompile "org.scalatest:scalatest_$scalacVersion:3.0.1"
    testRuntime "org.pegdown:pegdown:1.4.2" // used by scalatest plugin to generate html reports
}
